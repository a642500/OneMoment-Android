apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

apply from: '/home/yyz/OneMoment/private.gradle'

android {
    signingConfigs {
        debugSignature {
            keyAlias SIGNATURE.KEY_ALIAS_DEBUG
            keyPassword SIGNATURE.KEY_PASSWORD
            storeFile file(SIGNATURE.KEYSTORE_PATH)
            storePassword SIGNATURE.STORE_PASSWORD
        }
        releaseSignature {
            keyAlias SIGNATURE.KEY_ALIAS_RELEASE
            keyPassword SIGNATURE.KEY_PASSWORD
            storeFile file(SIGNATURE.KEYSTORE_PATH)
            storePassword SIGNATURE.STORE_PASSWORD
        }
    }

    compileSdkVersion 22
    buildToolsVersion "23.0.0 rc3"

    defaultConfig {
        applicationId "co.yishun.onemoment.app"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    aaptOptions {
        ignoreAssetsPattern 'x86:fonts:armeabi-v7a-neon'
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseSignature
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debugSignature
        }
    }

    productFlavors {
        uiAutomator { // for UIAutomation test, api > 18
            minSdkVersion 18
            targetSdkVersion 22
        }
        noFFmpeg {
            aaptOptions.ignoreAssetsPattern 'x86:fonts:armeabi-v7a-neon:armeabi:armeabi-v7a'
        }
        preview {} // for universal and test volunteers preview
        // for release
        market360 {}
        marketWandoujia {}
        marketBaidu {}
        marketCoolapk {}
        marketGooglePlay {}
        marketHuawei {}
        marketXiaomi {}
        marketYingyongbao {}
        marketUniversal {}// for other market
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // https://developer.android.com/reference/android/support/annotation/package-summary.html
    compile 'com.android.support:support-annotations:22.2.0'
    /**
     *  Fragment
     *  NotificationCompat 16
     *  LocalBroadcastManager
     *
     *  ViewPager
     *  DrawerLayout
     *
     *  x   Loader  11
     *  x   FileProvider
     *  x   ShareActionProvider 14
     */
    compile 'com.android.support:support-v4:22.2.0'
    /**
     *  X   ActionBar
     * AppCompatActivity
     * AppCompatDialog
     *  X   ShareActionProvider
     */
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:cardview-v7:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.android.support:palette-v7:22.2.0'
    compile 'com.android.support:design:22.2.0'

    compile 'com.google.guava:guava:18.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.google.code.gson:gson:2.3.1'

    androidTestCompile 'com.android.support.test:runner:0.3'
    androidTestCompile 'com.android.support.test:rules:0.3'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2'
    androidTestUiAutomatorCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.1'

}

apply plugin: 'aar-link-sources'

dependencies {
    aarLinkSources 'com.android.support.test.espresso:espresso-core:2.2:sources@jar'
    aarLinkSources 'com.android.support:recyclerview-v7:22.2.0:sources@jar'
    // not available    aarLinkSources 'com.android.support:design:22.2.0:sources@jar'
}