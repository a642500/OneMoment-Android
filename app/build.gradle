apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'


Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    signingConfigs() {
        debugSignature {
            keyAlias = properties.getProperty("KEY_ALIAS_DEBUG")
            keyPassword properties.getProperty("KEY_PASSWORD")
            storeFile file(properties.getProperty("KEYSTORE_PATH"))
            storePassword properties.getProperty("STORE_PASSWORD")
        }
        releaseSignature {
            keyAlias properties.getProperty("KEY_ALIAS_RELEASE")
            keyPassword properties.getProperty("KEY_PASSWORD")
            storeFile file(properties.getProperty("KEYSTORE_PATH"))
            storePassword properties.getProperty("STORE_PASSWORD")
        }
    }

    compileSdkVersion 23
    buildToolsVersion "23.0.0"

    defaultConfig {
        applicationId "co.yishun.onemoment.app"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    aaptOptions {
        ignoreAssetsPattern 'x86:fonts:armeabi-v7a-neon'
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    buildTypes() {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
//            signingConfig signingConfigs.releaseSignature
        }
        debug {
            minifyEnabled false
//            signingConfig signingConfigs.debugSignature
        }
    }

    productFlavors {
        uiAutomator { // for UIAutomation test, api > 18
            minSdkVersion 18
            targetSdkVersion 23
        }
        noFFmpeg {
            aaptOptions.ignoreAssetsPattern 'x86:fonts:armeabi-v7a-neon:armeabi:armeabi-v7a'
        }
        preview {} // for universal and test volunteers preview
        // for release
        market360 {}
        marketWandoujia {}
        marketBaidu {}
        marketCoolapk {}
        marketGooglePlay {}
        marketHuawei {}
        marketXiaomi {}
        marketYingyongbao {}
        marketUniversal {}// for other market
    }
}

def SUPPORT_VERSION = '23.0.0'

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'net.sourceforge.streamsupport:streamsupport:1.2.2'
    // https://developer.android.com/reference/android/support/annotation/package-summary.html
    compile "com.android.support:support-annotations:$SUPPORT_VERSION"
    /**
     *  Fragment
     *  NotificationCompat 16
     *  LocalBroadcastManager
     *
     *  ViewPager
     *  DrawerLayout
     *
     *  x   Loader  11
     *  x   FileProvider
     *  x   ShareActionProvider 14
     */
    compile "com.android.support:support-v4:$SUPPORT_VERSION"
    /**
     *  X   ActionBar
     * AppCompatActivity
     * AppCompatDialog
     *  X   ShareActionProvider
     */
    compile "com.android.support:appcompat-v7:$SUPPORT_VERSION"
    compile "com.android.support:cardview-v7:$SUPPORT_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_VERSION"
    compile "com.android.support:palette-v7:$SUPPORT_VERSION"
    compile "com.android.support:design:$SUPPORT_VERSION"
//TODO bug https://code.google.com/p/android/issues/detail?id=180462

    compile 'com.google.guava:guava:18.0'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.squareup.picasso:picasso:2.5.2'

    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.4.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'

    // provided 'de.greenrobot:eventbus-annotation-processor:3.0.0-beta1'
    // http://androiddevblog.com/eventbus-3-droidcon/
    compile 'de.greenrobot:eventbus:3.0.0-beta1'
    compile 'com.birbit:android-priority-jobqueue:1.3.2'
    // compile 'de.greenrobot:greendao:1.3.7' no need because Realm
    // provided 'de.greenrobot:greendao-generator:1.3.1' for another project
    // http://greendao-orm.com/documentation/introduction/

    androidTestCompile 'com.android.support.test:runner:0.3'
    androidTestCompile 'com.android.support.test:rules:0.3'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2'
    androidTestUiAutomatorCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.1'

    compile 'com.github.hiteshsondhi88.libffmpeg:FFmpegAndroid:0.2.5'
    compile 'com.umeng.analytics:analytics:5.5.3'
    compile 'com.qiniu:qiniu-android-sdk:7.0.6.1'
    compile 'io.realm:realm-android:0.82.2'
    // for compatible
    compile 'com.j256.ormlite:ormlite-core:4.48'
    compile 'com.j256.ormlite:ormlite-android:4.48'

    compile 'com.soundcloud.android:android-crop:0.9.10@aar'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.github.andkulikov:transitions-everywhere:1.4.0'
    compile 'com.daimajia.slider:library:1.1.5@aar'
    compile 'com.daimajia.easing:library:1.0.1@aar'
    compile 'com.daimajia.androidanimations:library:1.1.3@aar'
    compile 'com.daimajia.androidviewhover:library:1.0.4@aar'
    compile 'nl.codesoup.cubicbezier:cubicbezier:1.0.0'
    compile 'de.hdodenhof:circleimageview:1.3.0'
    compile 'com.afollestad:material-dialogs:0.7.8.1'
    compile 'com.malinskiy:superrecyclerview:1.1.1'
}

apply plugin: 'aar-link-sources'

dependencies {
    aarLinkSources 'com.android.support.test.espresso:espresso-core:2.2:sources@jar'
    aarLinkSources 'com.android.support:recyclerview-v7:22.2.0:sources@jar'
    aarLinkSources 'com.daimajia.slider:library:1.1.5:sources@jar'
    aarLinkSources 'com.malinskiy:superrecyclerview:1.1.1:sources@jar'
}

apply plugin: 'android-apt'

dependencies {
    apt "org.androidannotations:androidannotations:3.3.2"
    compile "org.androidannotations:androidannotations-api:3.3.2"
}

apt {
    arguments {
        androidManifestFile variant.outputs[0].processResources.manifestFile
        // if you have multiple outputs (when using splits), you may want to have other index than 0

        // you should set your package name here if you are using different application IDs
        // resourcePackageName "your.package.name"

        // You can set optional annotation processing options here, like these commented options:
        // logLevel 'INFO'
        // logFile '/var/log/aa.log'
    }
}

repositories {
    maven {
        url "https://jitpack.io"
    }
}

dependencies {
    compile project(':dragger')
//    compile 'com.github.ppamorim:dragger:1.1'
    compile 'com.jenzz:materialpreference:1.3'
}

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    compile 'org.lucasr.twowayview:core:1.0.0-SNAPSHOT@aar'
    compile 'org.lucasr.twowayview:layouts:1.0.0-SNAPSHOT@aar'
}

dependencies {
    compile('com.github.ozodrukh:CircularReveal:1.1.1@aar') {
        transitive = true;
    }
}
// https://www.reddit.com/r/androiddev/comments/3dnibt/support_library_2221_released/
configurations.all() {
    // Currently espresso is dependent on support-annotations:22.2.0
    resolutionStrategy.force 'com.android.support:support-annotations:23.0.0'
}